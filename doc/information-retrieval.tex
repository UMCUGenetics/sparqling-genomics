\chapter{Information retrieval with SPARQL}
\label{chap:information-retrieval}

  In section \refer{sec:vcf2rdf} we discussed how to extract triples from
  common data formats.  In section \refer{sec:curl} we discussed how we
  could insert those triples into a SPARQL endpoint.

  In this section, we will start exploring the inserted data by using a
  query language called \i{SPARQL}.  Understanding SPARQL will be crucial
  for the integration in our own programs or scripts --- something we will
  discuss in chapter \refer{chap:programming}.

  The queries in the remainder of this chapter can be readily copy/pasted into
  the query editor of the web interface (see chapter
  \refer{chap:web-interface}).

\section{Local querying}

  When we request information from a SPARQL endpoint, we are performing a
  \i{local query} because we request data from a single place.  In our case,
  that is most likely to be our own SPARQL endpoint.

  In contrast to \i{local querying}, we can also query multiple SPARQL
  endpoints in one go, to combine the information from multiple locations.
  Combining information from multiple SPARQL endpoints is called \i{federated
    querying}.

  Federated querying is discussed in section \refer{sec:federated-querying}.

\subsection{Listing non-empty graphs}
\label{sec:non-empty-graphs}
  Each SPARQL endpoint can host multiple \i{graphs}.  Each graph can contain
  an independent set of triples.  The following query displays the available
  non-empty graphs in a SPARQL endpoint:

\begin{lstlisting}[language=SPARQL]
SELECT DISTINCT ?graph WHERE { GRAPH ?graph { ?s ?p ?o } }
\end{lstlisting}

Which may yield the following table:

\begin{table}[H]
  \begin{tabularx}{\textwidth}{ L }
    \headrow
    \b{graph}\\
    \evenrow
    \t{http://example}\\
    \oddrow
    \t{http://localuriqaserver/sparql}\\
    \evenrow
    \t{http://www.openlinksw.com/schemas/virtrdf\#}\\
    \oddrow
    \t{http://www.w3.org/2002/07/owl\#}\\
    \evenrow
    \t{http://www.w3.org/ns/ldp\#}\\
  \end{tabularx}
  \caption{\small Results of the query to list non-empty graphs.}
  \label{table:query-output-1}
\end{table}

  The graph names usually look like URLs, like we would encounter them on the
  web.  In fact, not only graph names, but any node that has a symbolic meaning,
  rather than a literal\footnote{Examples of literals are numbers and strings.
    Symbols are nodes that don't have a literal value.} meaning is usually
  written as a URL.  We can go to such a URL with a web browser and might even
  find more information.

\subsection{Querying a specific graph}

  The sooner we can reduce the dataset to query over, the faster the query will
  return with an answer.  One easy way to reduce the size of the dataset is to
  be specific about which graph to query.  This can be achieved using the
  \t{FROM} clause in the query.

\begin{lstlisting}[language=SPARQL]
SELECT ?s ?p ?o
FROM <graph-name>
WHERE { ?s ?p ?o }
\end{lstlisting}

  The \t{graph-name} must be one of the graphs returned by the query from
  section \refer{sec:non-empty-graphs}.

  Without the \t{FROM} clause, the RDF store will search in all graphs.
  We can repeat the \t{FROM} clause to query over multiple graphs in the
  same RDF store.

\begin{lstlisting}[language=SPARQL]
SELECT ?s ?p ?o
FROM <graph-name>
FROM <another-graph-name>
WHERE { ?s ?p ?o }
\end{lstlisting}

  In section \refer{sec:federated-querying} we will look at querying over
  multiple RDF stores.

\subsection{Exploring the structure of knowledge in a graph}


  Inside the \t{WHERE} clause of a SPARQL query we specify a graph
  pattern.  When the information in a graph is structured, there are only few
  predicates in comparison to the number of subjects and the number of objects.

\begin{lstlisting}[language=SPARQL]
SELECT COUNT(DISTINCT ?s) AS ?subjects
       COUNT(DISTINCT ?p) AS ?predicates
       COUNT(DISTINCT ?o) AS ?objects
FROM <http://example>
WHERE { ?s ?p ?o }
\end{lstlisting}

On a typical graph with data originating from \t{vcf2rdf}, this may yield
the following table:

\begin{table}[H]
  \begin{tabularx}{\textwidth}{*{3}{!{\VRule[-1pt]}X}}
    \headrow
    \b{subjects} & \b{predicates} & \b{objects}\\
    \evenrow
    \t{3011691} & \t{229} & \t{4000809}\\
  \end{tabularx}
  \caption{\small Results of the query to count the number of subjects,
    predicates, and objects in a graph.}
  \label{table:query-output-2}
\end{table}

  Therefore, one useful method of finding out which patterns exist in a
  graph is to look for predicates:

\begin{lstlisting}[language=SPARQL]
SELECT DISTINCT ?predicate
FROM <http://example>
WHERE {
  ?subject ?predicate ?object .
}
\end{lstlisting}

  Which may yield the following table:

\begin{table}[H]
  \begin{tabularx}{\textwidth}{ L }
    \headrow
    \b{predicate}\\
    \evenrow
    \t{http://biohackathon.org/resource/faldo\#position}\\
    \oddrow
    \t{http://biohackathon.org/resource/faldo\#reference}\\
    \evenrow
    \t{http://sparqling-genomics/vcf2rdf/filename}\\
    \oddrow
    \t{http://sparqling-genomics/vcf2rdf/foundIn}\\
    \evenrow
    \t{http://sparqling-genomics/vcf2rdf/sample}\\
    \oddrow
    \t{http://sparqling-genomics/vcf2rdf/VariantCall/ALT}\\
    \evenrow
    \t{http://sparqling-genomics/vcf2rdf/VariantCall/FILTER}\\
    \oddrow
    $\ldots$\\
  \end{tabularx}
  \caption{\small Results of the query to list predicates.}
  \label{table:query-output-3}
\end{table}

\subsection{Listing samples and their originating files}

Using the knowledge we gained from exploring the predicates in a graph,
we can construct more insightful queries, like finding the names of the
samples and their originating filenames from the output of \t{vcf2rdf}:

\begin{lstlisting}[language=SPARQL]
PREFIX vcf2rdf: <http://sparqling-genomics/vcf2rdf/>

SELECT DISTINCT STRAFTER(STR(?sample), "Sample/") AS ?sample ?filename
FROM <graph-name>
WHERE {
  ?variant  vcf2rdf:sample    ?sample .
  ?sample   vcf2rdf:foundIn   ?origin .
  ?origin   vcf2rdf:filename  ?filename .
}
\end{lstlisting}

Which may yield the following table:

\begin{table}[H]
  \begin{tabularx}{\textwidth}{l!{\VRule[-1pt]}L}
    \headrow
    \b{sample} & \b{filename}\\
    \evenrow
    \t{REF0047} & \t{/data/examples/TUMOR\_REF0047.annotated.vcf.gz}\\
    \oddrow
    \t{TUMOR0047} & \t{/data/examples/TUMOR\_REF0047.annotated.vcf.gz}\\
    \evenrow
    $\ldots$ & $\ldots$\\
  \end{tabularx}
  \caption{\small Results of the query to list samples and their originating
    filenames.}
  \label{table:query-output-4}
\end{table}

  Notice how most predicates for \t{vcf2rdf} in table
  \ref{table:query-output-3} start with
  \t{http://sparqling-genomics/vcf2rdf/}.  In the above query, we used
  this to shorten the query.  We started the query by writing a \t{PREFIX}
  rule for \t{http://sparqling-genomics/vcf2rdf/}, which we called
  \t{vcf2rdf:}.  This means that whenever we write \t{vcf2rdf:FOO}, the SPARQL endpoint interprets
  it as if we would write\\\t{<http://sparqling-genomics/vcf2rdf/FOO>}.

  We will use more prefixes in the upcoming queries.  We can look up prefixes
  for common ontologies using \href{http://prefix.cc}{http://prefix.cc}.

\subsection{Listing samples, originated files, and number of variants}

Building on the previous query, and by exploring the predicates of a
\t{vcf2rdf:VariantCall}, we can construct the following query to
include the number of variants for each sample, in each file.

\begin{lstlisting}[language=SPARQL]
PREFIX vcf2rdf: <http://sparqling-genomics/vcf2rdf/>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT DISTINCT STRAFTER(STR(?sample), "Sample/") AS ?sample
       ?filename
       COUNT(DISTINCT ?variant) AS ?numberOfVariants

FROM <graph-name>
WHERE
{
  ?variant  rdf:type                vcf2rdf:VariantCall ;
            vcf2rdf:sample          ?sample ;
            vcf2rdf:originatedFrom  ?origin .

  ?origin   vcf2rdf:filename        ?filename .
}
\end{lstlisting}

  Which may yield the following table:

  \begin{table}[H]
    \begin{tabularx}{\textwidth}{ l!{\VRule[-1pt]}L!{\VRule[-1pt]}l }
      \headrow
      \b{sample} & \b{filename} & \b{numberOfVariants}\\
      \evenrow
      \t{REF0047}   & \t{/data/examples/TUMOR\_REF0047.annotated.vcf.gz} & \t{1505712}\\
      \oddrow
      \t{TUMOR0047} & \t{/data/examples/TUMOR\_REF0047.annotated.vcf.gz} & \t{1505712}\\
      \evenrow
      $\ldots$ & $\ldots$ & $\ldots$\\
    \end{tabularx}
    \caption{\small Results of the query to list samples, their originated
      filenames, and the number of variant calls for each sample in a file.}
    \label{table:query-output-5}
  \end{table}

  By using \t{COUNT}, we can get the \t{DISTINCT} number of
  matching patterns for a variant call for a sample, originating from
  a distinct file.

\subsection{Retrieving all variants}

  When retrieving potentially large amounts of data, the \t{LIMIT}
  clause may come in handy to prototype a query until we are sure enough
  that the query answers the actual question we would like to answer.

  In the next example query, we will retrieve the sample name,
  chromosome, position, and the corresponding VCF \t{FILTER} field(s)
  from the database.

\begin{lstlisting}[language=SPARQL]
PREFIX vcf2rdf: <http://sparqling-genomics/vcf2rdf/>
PREFIX vc:      <http://sparqling-genomics/vcf2rdf/VariantCall/>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX faldo:   <http://biohackathon.org/resource/faldo#>

SELECT DISTINCT ?variant ?sample ?chromosome ?position ?filter
FROM <graph-name>
WHERE
{
  ?variant  rdf:type                vcf2rdf:VariantCall ;
            vcf2rdf:sample          ?sample ;
            faldo:reference         ?chromosome ;
            faldo:position          ?position ;
            vc:FILTER               ?filter .
}
LIMIT 100
\end{lstlisting}

  By limiting the result set to the first 100 rows, the query will return
  with an answer rather quickly.  Had we not set a limit to the number of
  rows, the query could have returned possibly a few million rows, which
  would obviously take longer to process.

\subsection{Retrieving variants with a specific mutation}

  Any property can be used to subset the results.  For example, we can
  look for occurrences of a \t{C} to \t{T} mutation in the positional
  range $202950000$ to $202960000$ on chromosome \t{2}, according to the
  \i{GRCh37 (hg19)} reference genome with the following query:

\begin{lstlisting}[language=SPARQL]
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX faldo: <http://biohackathon.org/resource/faldo#>
PREFIX hg19:  <http://rdf.biosemantics.org/data/genomeassemblies/hg19#>
PREFIX v:     <http://sparqling-genomics/vcf2rdf/>
PREFIX vc:    <http://sparqling-genomics/vcf2rdf/VariantCall/>
PREFIX seq:   <http://sparqling-genomics/vcf2rdf/Sequence/>

SELECT COUNT(DISTINCT ?variant) AS ?occurrences ?sample
FROM <http://example>
WHERE {
  ?variant  rdf:type         v:VariantCall .
  ?variant  rdf:type         ?genotype .
  ?variant  v:sample         ?sample .
  ?variant  vc:REF           seq:C .
  ?variant  vc:ALT           seq:T .
  ?variant  faldo:reference  hg19:chr2 .
  ?variant  faldo:position   ?position .

  FILTER (?position >= 202950000)
  FILTER (?position <= 202960000)

  # Exclude variants that actually do not deviate from hg19.
  FILTER (?genotype != v:HomozygousReferenceGenotype)
}
LIMIT 2
\end{lstlisting}

Which may yield the following table:

\begin{table}[H]
  \begin{tabularx}{\textwidth}{ l!{\VRule[-1pt]}L }
    \headrow
    \b{occurrences} & \b{sample}\\
    \evenrow
    \t{5} & \t{http://sparqling-genomics/vcf2rdf/Sample/REF0047}\\
    \oddrow
    \t{5} & \t{http://sparqling-genomics/vcf2rdf/Sample/TUMOR0047}\\
  \end{tabularx}
  \caption{\small Query results of the above query.}
  \label{table:query-output-6}
\end{table}

\subsection{Comparing two datasets on specific properties}

  Suppose we run variant calling on the same sample with slightly different
  analysis programs.  We expect a large overlap in variants between the
  datasets, and would like to view the few variants that differ in each dataset.

  We imported each dataset in a separate graph (\t{http://comparison/aaa}
  and \t{http://comparison/bbb}).

  The properties we are going to compare are the predicates \t{faldo:reference},
  \t{faldo:position}, \t{vc:REF}, and \t{vc:ALT}.

  The query below displays how each variant in \t{http://comparison/aaa}
  can be compared to a matching variant in \t{http://comparison/bbb}.
  Only those variants in \t{http://comparison/aaa} that \b{do not}
  have an equivalent variant in \t{http://comparison/bbb} will be
  returned by the SPARQL endpoint.

\begin{lstlisting}[language=SPARQL]
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX faldo:   <http://biohackathon.org/resource/faldo#>
PREFIX vcf2rdf: <http://sparqling-genomics/vcf2rdf/>
PREFIX vc:      <http://sparqling-genomics/vcf2rdf/VariantCall/>

SELECT DISTINCT
  STRAFTER(STR(?chromosome), "hg19#") AS ?chromosome
  ?position
  STRAFTER(STR(?reference), "Sequence/") AS ?reference
  STRAFTER(STR(?alternative), "Sequence/") AS ?alternative
  STRAFTER(STR(?filter), "vcf2rdf/") AS ?filter
WHERE
{
  GRAPH <http://comparison/aaa>
  {
    ?aaa_variant  rdf:type         vcf2rdf:VariantCall ;
                  vc:REF           ?reference ;
                  vc:ALT           ?alternative ;
                  vc:FILTER        ?filter ;
                  faldo:reference  ?chromosome ;
                  faldo:position   ?position .
  }

  MINUS
  {
    GRAPH <http://comparison/bbb>
    {
      ?variant  rdf:type         vcf2rdf:VariantCall ;
                vc:REF           ?reference ;
                vc:ALT           ?alternative ;
                faldo:reference  ?chromosome ;
                faldo:position   ?position .
    }
  }
}
\end{lstlisting}

  So the \t{MINUS} construct in SPARQL can be used to filter overlapping
  information between multiple graphs.

  This query demonstrates how a fine-grained ``diff'' can be constructed
  between two datasets.

\section{Federated querying}
\label{sec:federated-querying}

  Now that we've seen local queries, there's only one more construct we need to
  know to combine this with remote SPARQL endpoints: the \t{SERVICE}
  construct.

  For the next example, we will use the \href{http://www.ebi.ac.uk/rdf/services/sparql}%
  {public SPARQL endpoint hosted by EBI}.

\subsection{Get an overview of Biomodels (from ENSEMBL)}

\begin{lstlisting}[language=SPARQL]
PREFIX sbmlrdf: <http://identifiers.org/biomodels.vocabulary#>
PREFIX sbmldb:  <http://identifiers.org/biomodels.db/>

SELECT ?speciesId ?name {
  SERVICE <http://www.ebi.ac.uk/rdf/services/sparql/> {
    sbmldb:BIOMD0000000001 sbmlrdf:species ?speciesId .
    ?speciesId sbmlrdf:name ?name
  }
}
\end{lstlisting}

Which may yield the following table:

\begin{table}[H]
  \begin{tabularx}{\textwidth}{ l!{\VRule[-1pt]}L }
    \headrow
    \b{speciesId} & \b{name}\\
    \evenrow
    \t{http://identifiers.org/biomodels.db/BIOMD0000000001\#\_000003} & \t{BasalACh2}\\
    \oddrow
    \t{http://identifiers.org/biomodels.db/BIOMD0000000001\#\_000004} & \t{IntermediateACh}\\
    \evenrow
    \t{http://identifiers.org/biomodels.db/BIOMD0000000001\#\_000005} & \t{ActiveACh}\\
    \oddrow
    \t{http://identifiers.org/biomodels.db/BIOMD0000000001\#\_000006} & \t{Active}\\
    \evenrow
    \t{http://identifiers.org/biomodels.db/BIOMD0000000001\#\_000007} & \t{BasalACh}\\
    \oddrow
    $\ldots{}$ & $\ldots{}$\\
  \end{tabularx}
  \caption{\small Query results of the above query.}
  \label{table:query-output-7}
\end{table}

\section{Tips and tricks for writing portable queries}
\label{sec:portable-queries}

  While SPARQL has a formal standard specification, due to the different
  implementations of RDF stores, a query may sometimes produce an error
  on one endpoint, and a perfectly fine answer on another.

  In this chapter we discuss ways to write ``portable'' queries, so that
  the queries can be run equally on each type of endpoint.

\section{Provide names for aggregated columns}

  When using aggregated results in a column, for example by using the
  \t{COUNT} or \t{SUM} functions, always provide a name for
  the column.  Let's take a look at the following example:

\begin{lstlisting}[language=SPARQL]
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>

SELECT DISTINCT ?cause COUNT(?cause)
WHERE {
  ?human  wdt:P31    wd:Q5     ;        # Instance of human
          wdt:P509   ?cid      .        # Cause of death
  ?cid    wdt:P279*  wd:Q12078 .        # Type of cancer

  SERVICE wikibase:label
  {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE],nl" .
    ?cid rdfs:label ?cause .
  }
}
GROUP BY ?cause
\end{lstlisting}

  This query displays number of occurrences, and the causes of
  death for humans known to Wikipedia, limited to cancer.  The two
  columns are specified in the following line:

\begin{lstlisting}[language=SPARQL]
SELECT DISTINCT ?cause COUNT(?cause)
\end{lstlisting}

  The first column will be named ``cause'', but what about the second?
  Some endpoints will automatically assign a unique name to the column,
  but others do not, and respond with an error.

  To avoid this, always provide a name for such a column by using the
  \t{AS} keyword.  The following line displays its usage:

\begin{lstlisting}[language=SPARQL]
SELECT DISTINCT ?cause (COUNT(?cause) AS ?occurrences)
\end{lstlisting}

  In addition to using the \t{AS} keyword, also wrap the statement in
  parentheses, so that the SPARQL interpreter can determine which name
  should be assigned to which column.

  Our final query looks like this:

\begin{lstlisting}[language=SPARQL]
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>

SELECT DISTINCT ?cause (COUNT(?cause) AS ?occurrences)
WHERE {
  ?human  wdt:P31    wd:Q5     ;        # Instance of human
          wdt:P509   ?cid      .        # Cause of death
  ?cid    wdt:P279*  wd:Q12078 .        # Type of cancer

  SERVICE wikibase:label
  {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE],nl" .
    ?cid rdfs:label ?cause .
  }
}
GROUP BY ?cause
\end{lstlisting}
